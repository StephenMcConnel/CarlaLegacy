c MasterToXml.cct

begin	> '<?xml version="1.0" encoding="UTF-8"?>' nl
	  '<!DOCTYPE masterGrammar SYSTEM "MasterGrammar.dtd">' nl
	  '<masterGrammar>' nl
	  clear(DoingRule)
	  use(Main,Wedges)

define(EndRule) > '	&lt;' out(Phrase) ' ' out(Option) '&gt; = ' out(Code) nl
				  '</rule>' nl
				  '<showWhen id="' out(ShowWhen) '">' nl
		  '<whenValue element="" attr="" value="" negateValue="no"/>' nl
				  '</showWhen>' nl


group(Main)

'|'	>  ifn(DoingRule)
			 begin
			  '<!-- ' dup
			  use(EndOfComment,Wedges)
			 end
		   else
			 begin
			   dup
			 end

nl 'rule'	> if(DoingRule)
			begin
			  do(EndRule)
			end
		   nl '<rule show="' store(RuleBegin) 'rule'
		   set(DoingRule)
				   use(RuleBegin,Wedges)

endfile 	> if(DoingRule)
			begin
			  do(EndRule)
			end
		   '</masterGrammar>' nl
		   endfile

group(RuleBegin)

'{'	> dup store(Phrase) use(Phrase,Wedges)

group(Phrase)

"'"	> 'Bar'
' '	> append(RuleBegin) outs(Phrase) dup store(Option) use(Option,Wedges)

group(Option)

' '	> append(RuleBegin) outs(Option) dup store(Code) use(Code,Wedges)

group(Code)

' '	> append(RuleBegin) outs(Code) dup
	  store(ShowWhen)
	  outs(Phrase,Option,Code)
	  endstore
	  out(ShowWhen)
	  '">' nl
	  out(RuleBegin)
	  use(Main,Wedges)

group(EndOfComment)

nl	> ' -->' dup use(Main,Wedges)

group(Wedges)

'&'	> '&amp;'
'<'	> '&lt;'
'>'	> '&gt;'
